buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url artifactoryURL
            credentials {
                username = artifactoryUser
                password = artifactoryPassword
            }
        }
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.4.2'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    }

}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'net.ltgt.errorprone'
apply from: "$rootDir/gradle/artifacts.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/pmd.gradle"
apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/findbugs.gradle"
apply from: "$rootDir/gradle/publishing.gradle"

group = 'com.pega.gcs'
version = '3.2-SNAPSHOT'
description = "Pega-TracerViewer"

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "com.pega.gcs.tracerviewer.TracerViewer"

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
     maven { url "http://dl.bintray.com/pegasystems/fringeutils" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'dom4j', name: 'dom4j', version:'1.6.1'
    compile group: 'gnu.getopt', name: 'java-getopt', version:'1.0.13'
    compile group: 'commons-io', name: 'commons-io', version:'2.5'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version:'2.8.2'
    compile group: 'com.pega.gcs', name: 'fringeutils-common', version:'3.1'
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.0'
    compile group: 'net.sf.flexjson', name: 'flexjson', version: '3.3'
    compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.3'
    testCompile "junit:junit:4.12"
    testCompile ('org.assertj:assertj-core:3.8.0')
}

tasks.withType(JavaCompile) {
    // Disabled '-Werror'
    // ,'-XepAllDisabledChecksAsWarnings'
    // ,'-Xep:Var:OFF'
    options.compilerArgs += ['-Xlint:-options']
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

tasks.withType (Test) {
    maxParallelForks = 2
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/${name}.exec")
        classDumpDir = file("$buildDir/jacoco/${name}_ClassDump")
    }
}

test {
    description 'Used for running unit tests.'
    exclude '**/**MockTest.class'
    exclude '**/**IntegTest.class'
}

task mockTest(type: Test) {
    description 'Used for running mock tests.'
    include '**/**MockTest.class'
}

task integTest(type: Test) {
    description 'Used for running integ tests.'
    include '**/**IntegTest.class'
}

jar {
    manifest {
        attributes (
                'Main-Class': 'com.pega.gcs.tracerviewer.TracerViewer',
        )
    }
}

pmd {
    ignoreFailures = true
}

findbugs {
    ignoreFailures = true
}

build.finalizedBy jacocoTestReport, jacocoTestCoverageVerification